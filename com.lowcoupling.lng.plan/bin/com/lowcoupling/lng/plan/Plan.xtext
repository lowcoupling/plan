grammar com.lowcoupling.lng.plan.Plan with org.eclipse.xtext.common.Terminals hidden(WS, SL_COMMENT)

generate plan "http://www.lowcoupling.com/lng/plan/Plan"

Program:
	(description=ML_COMMENT)?
	('Program' name = ID OPENCURLY	
		 ('name' longName= STRING ENDLINE)?
		(plans += PlanImport ENDLINE)*
		('project' referencedProjects+=[Project|QualifiedName] ENDLINE)*
		projects+=Project*
	CLOSEDCURLY)
	|projects+=Project
;

Project:
	(description=ML_COMMENT)?
	'Project' name = ID OPENCURLY
		 ('name' longName= STRING ENDLINE)?
		 (resources = Resources)?
		 (plans += PlanImport ENDLINE)*
		('assumptions' OPENCURLY assumptions+=Assumption* CLOSEDCURLY)?
		('constraints' OPENCURLY constraints+=Constraint* CLOSEDCURLY)?
		((activities+=ActivityElement)*
			
		)
	CLOSEDCURLY
;

Assumption:
	'-' value = STRING ENDLINE
;
Constraint:
	'-' value = STRING ENDLINE
;

ActivityGroup:
	(description=ML_COMMENT)?
	'Group'name=ID OPENCURLY
	('name' longName= STRING ENDLINE)?
	('dependsOn' dependencies += ([ActivityElement|QualifiedName])*ENDLINE)?
	activities += ActivityElement*
		//groups += ActivityGroup*
	CLOSEDCURLY
;
WBSImport :
 'wbs' importURI=STRING  // feature must be named importURI
 ;
 
ResourcesImport :
 'resources' importURI=STRING  // feature must be named importURI
 ;
 
PlanImport :
 'plan' importURI=STRING  // feature must be named importURI
 ;
 
QualifiedName:
    ID ('.' ID)*;

QualifiedNameWithWildCard: QualifiedName('.*')?;
	
ActivityElement:
	Activity|
	CheckPoint|
	ActivityGroup
;

ENDLINE:
	";"
;

Resources: 'Resources' OPENCURLY
	(resource += Resource)*
	CLOSEDCURLY
;

Resource:
	'Resource' name= STRING ENDLINE
;

CheckPoint:
	(description=ML_COMMENT)?
	'CheckPoint' name= ID OPENCURLY
	('name' longName= STRING ENDLINE)?
	(
	(('date'  end = STRING ENDLINE) | ('after' after = [ActivityElement|QualifiedName] 'offset' offset = INT ENDLINE)) &
	'completeness' completeness = INT '%' ENDLINE
	('dependsOn' dependencies += ([ActivityElement|QualifiedName])*ENDLINE)?
	)
	CLOSEDCURLY
;
ResourceInvolvement:
	 resource=[Resource] ('at' occupation=INT'%')? ('as' responsibility=Responsibility)?
	 ENDLINE
;
 
Activity :
	(description=ML_COMMENT)?
	'Activity' name=ID OPENCURLY	
	('name' longName= STRING ENDLINE)?
	('involves' OPENCURLY (involvedResources+=ResourceInvolvement)* CLOSEDCURLY)?
	(
	 (('starts' start = STRING ENDLINE)
	 |('after' after=[ActivityElement|QualifiedName] 'offset' offset = INT ENDLINE)
	 )&
	('ends'	end = STRING ENDLINE|
	 'duration' duration = INT ENDLINE	
	)
	'completeness' completeness = INT'%' ENDLINE& 
	('dependsOn' dependencies += ([ActivityElement|QualifiedName])*ENDLINE)?
	)
	CLOSEDCURLY
;

Date:
	INT'/'INT'/'INT
;

terminal OPENCURLY :
	'{'
;
terminal CLOSEDCURLY :
	'}'
;
enum Responsibility:
	RESPONSIBLE|ACCOUNTABLE|CONSULTED|INFORMED
;